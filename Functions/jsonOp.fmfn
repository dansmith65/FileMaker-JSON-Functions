/**
 * =====================================
 * jsonOp ( key ; value )
 *
 * PURPOSE:
 *		Encodes a key value pair as an JSON object
 *
 * PARAMETERS:
 *		key = the name of the property
 *		value = any value to be encoded
 *
 * DEPENDENCIES:
 *		Custom Functions:
 *			z_jsonEncodeSupport
 *
 * LICENSE:
 *		See the LICENSE.md file for license rights and limitations (MIT):
 *		https://raw.githubusercontent.com/dansmith65/FileMaker-JSON-Functions/master/LICENSE.md
 *
 * LINK: https://github.com/dansmith65/FileMaker-JSON-Functions
 *
 * HISTORY:
 *		v1.0.0 RELEASED on 2015-APR-27 by Daniel Smith dansmith65@gmail.com
 * =====================================
 */

z_jsonEncodeSupport ( 2 /* String */ ; key )
& ":"
& If (
	/* return the value as-is if it's a json array or object */
	/**
	 * TODO: this would incorrectly detect json on a value like:
	 *	{this is a note in curly-brances}
	 * TODO: would also fail if there is leading/trailing whitespace
	 */
	Let ( [
		~firstAndLast = Left ( value ; 1 ) & Right ( value ; 1 )
	] ;
		~firstAndLast = "[]"		/* array */
		or ~firstAndLast = "{}"		/* object */
	) ;
	value ;

	z_jsonEncodeSupport ( 1 /* Value */ ; value )
)
& ","